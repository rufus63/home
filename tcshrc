
if (-d /usr/java/j2sdk1.4.1_01) then
    setenv JAVA_HOME /usr/java/j2sdk1.4.1_01
endif

if (-d /usr/java/j2sdk1.4.2_01) then
    setenv JAVA_HOME /usr/java/j2sdk1.4.2_01
endif

if (-d /usr/java/j2me_cldc) then
    setenv CLDC_HOME /usr/java/j2me_cldc
endif

if (-d /usr/java/midp2.0fcs) then
    setenv MIDP_HOME /usr/java/midp2.0fcs
endif

if (-d /usr/java/jakarta-tomcat-3.2.4) then
    setenv TOMCAT_HOME /usr/java/jakarta-tomcat-3.2.4
endif

if (-d /usr/local/jakarta-ant-1.5.1) then
    setenv ANT_HOME /usr/local/jakarta-ant-1.5.1
endif

if (-d $HOME/WTK2.0) then
    setenv WTK_HOME $HOME/WTK2.0
endif

if (-d $HOME/download/maven-1.0-beta-9) then
    setenv MAVEN_HOME $HOME/download/maven-1.0-beta-9
    set path=($path $MAVEN_HOME/bin)
endif

if ($?prompt) then
    alias l 'ls -oFh'
    alias ls 'ls -F'
    alias lt 'ls -oFht \!* | head'
    alias vi vim
    alias make 'make \!* |& tee /tmp/make.errors'
    alias ant 'ant -find build.xml \!* |& tee /tmp/errors.lk'
    alias e 'vi -q'
    alias notes 'vi $HOME/text/notes/notes.txt'

    #set mips_gdb=/usr/local/comp/mips-elf/gcc-3.0.4/bin/mips-elf-gdb
    #if (-xf $mips_gdb) then
        #alias gdbm $mips_gdb
    #endif
    #unset mips_gdb

    bindkey ^Z run-fg-editor
    bindkey ' ' magic-space

    set promptchars='%#'

    if ($HOSTNAME == "mvdev" || 1) then
        set prompt="%{^[[33;1m%}%m:%~ %#%{^[[0m%} "
    else
        set prompt='%B%m:%~ %# %b'
    endif

    # set the path and other host-specific things
    if ($HOSTNAME == "mvdev") then
        set path=($path /usr/opt/sybase/SYBSsa8/bin)
    else if ($HOSTNAME == "plunk.org") then
        unsetenv DISPLAY
    endif

    if ($?ANT_HOME) then
        set path=($path $ANT_HOME/bin)
    endif

    if ($?JAVA_HOME) then
        set path=($path $JAVA_HOME/bin)
    endif

    if ($?CLDC_HOME) then
        set path=($path $CLDC_HOME/bin/linux)
    endif

    if ($?MIDP_HOME) then
        set path=($path $MIDP_HOME/bin)
    endif

    set path=($path ~/bin /sbin /usr/sbin .)

    if (-x /usr/local/bin/p4) then
        alias po 'p4 opened -c default'
        alias pj 'p4 jobs -e "assignee=lawrence & ^status=closed"'
    endif

    stty erase 
    unset autologout
    set fignore=(.class .o .obj)
    set autolist

    complete cd 'p/1/d/'

    # complete the latest core file:
    complete gdb 'p/2/`ls -t1 core.* | head -1`/'
    complete /usr/bin/gdb 'p/2/`ls -t1 core.* | head -1`/'
endif

# we want this in scripts too
alias make 'make \!* |& tee /tmp/make.errors'

if (! $?HOSTNAME) then
    umask 22
else
    if ($HOSTNAME == "plunk.org") then
        umask 22
    else
        umask 2
    endif
endif
