# for all shells

# remove duplicates from path when elements are added:
typeset -U path

function rscreen()
{
    if (( $# != 1 )); then
        echo "Usage: rscreen <hostname>" 2>&1
        return 2
    fi
    ssh -At $1 screen -d -R
    chpwd
}

function rtmux()
{
    if (( $# != 1 )); then
        echo "Usage: rmux <hostname>" 2>&1
        return 2
    fi
    ssh -At $1 "(tmux has-session && tmux attach -d) || tmux new-session"
    chpwd
}

function tcmd()
{
    if (($# == 0)); then
        echo "Usage: tcmd [-o output] cmd ..." 2>&1
        return 1
    fi
    local outfile=/tmp/$(python -c 'import sys, urllib; print urllib.quote(" ".join(sys.argv[1:]))', $@).out
    mkdir -p $(dirname $outfile)
    cmd=$1
    shift
    $cmd "$@" 2>&1 | tee ${outfile}
}

# add element to path if it exists, returning 0 on success or 1 on failure
function add_to_path()
{
    if (($# == 0)); then
        echo "Usage: add_to_path <path>"
    else
        if [[ -d $1 ]]; then
            path=($path $1)
            return 0
        fi
    fi

    return 1
}

# prepend element in path if it exists, returning 0 on success or 1 on failure
function prepend_to_path()
{
    if (($# == 0)); then
        echo "Usage: prepend_to_path <path>"
    else
        if [[ -d $1 ]]; then
            path=($1 $path)
            return 0
        fi
    fi

    return 1
}

# for things like local installations of vim.  need to prepend to
# get in front of /usr/bin if it's set by the system.
prepend_to_path ~/usr/bin

# Use CollabNet svn that is keyring aware. This goes before /usr/bin.
#prepend_to_path /opt/CollabNet_Subversion/bin

prepend_to_path /usr/local/bin
prepend_to_path ~/bin
add_to_path /usr/java/default/bin
add_to_path /usr/local/mysql/bin
add_to_path /sbin
add_to_path /usr/sbin

[ -x /usr/libexec/java_home ] && export JAVA_HOME="$(/usr/libexec/java_home)"

if [ -d /usr/local/Cellar/ec2-api-tools/1.6.13.0/libexec ]; then
  export EC2_HOME=/usr/local/Cellar/ec2-api-tools/1.6.13.0/libexec
fi

# Nose configuration
export PY_LOG_FORMAT="%(asctime)s %(levelname).1s %(process)5d %(threadName)s/%(name)s:%(filename)s:%(lineno)d: %(message)s"

# add X11 bin if it exists
if ! add_to_path /usr/bin/X11; then
    add_to_path /usr/X11R6/bin
fi

# try to set the machine-specific home bin path
HOSTNAME=$(print -P '%m')
add_to_path ~/bin/$HOSTNAME

if (( $+HOSTTYPE )); then
    add_to_path ~/bin/$HOSTTYPE
fi
